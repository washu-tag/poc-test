plugins {
    id 'java'
    id 'checkstyle'
}

group = 'edu.washu.tag'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://maven.dcm4che.org/'
    }
    maven {
        url 'https://raw.github.com/nroduit/mvn-repo/master/' // needed for dcm4che
    }
}

final String vRestAssured = '5.5.0'
final String vJackson = '2.18.0'
final String vTestNg = '7.9.0'
final String vHapiHl7V2 = '2.5.1'
final String vCommonsIo = '2.17.0'
final String vDcm4che = '5.33.0'

dependencies {
    implementation("io.rest-assured:rest-assured:${vRestAssured}")
    implementation("com.fasterxml.jackson.core:jackson-databind:${vJackson}")
    implementation("org.testng:testng:${vTestNg}")
    implementation("ca.uhn.hapi:hapi-base:${vHapiHl7V2}")
    implementation("ca.uhn.hapi:hapi-structures-v281:${vHapiHl7V2}")
    implementation("commons-io:commons-io:${vCommonsIo}")
    implementation("org.dcm4che.tool:dcm4che-tool-storescu:${vDcm4che}")
}

java {
    sourceCompatibility = '21'
}

test {
    systemProperties System.getProperties()

    useTestNG() {
        testName = 'scout_tests'
        suiteName = testName
    }

    testLogging {
        events 'FAILED', 'PASSED', 'SKIPPED', 'STANDARD_OUT', 'STARTED'
    }
}

checkstyle {
    toolVersion = '10.18.1'
    configFile = file('../custom_linting.xml')
}

tasks.register('aggregateTests', TestReport) {
    destinationDirectory = reporting.baseDirectory.dir('allTests')
    final File[] previousResultBinaries = file('test_result_binaries').listFiles()
    if (previousResultBinaries != null) {
        testResults.from(previousResultBinaries)
    }
}

tasks.register('aggregateLogs', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'edu.washu.tag.reporting.TestResultAggregator'
    args(file('test_results'))
}
